grammar it.marcotinacci.xturing.XTuring with org.eclipse.xtext.common.Terminals

generate xTuring "http://www.marcotinacci.it/xturing/XTuring"

Machine:
	'alphabet' '{' (symbols+=Symbol (',' symbols+=Symbol)*)? '}'
	(states+=State)+
;

Symbol: name=SYMBOL_ID;

State: 
	(beginState?='begin')? 'state' name=ID '{'
		(transactions+=Transaction)*
	'}'
;

Transaction:
	{Transaction}
	(reading?='read' (read+=ReadWriteSymbol)+)?
	(writing?='write' (write+=ReadWriteSymbol)+)?
	(moving?='move' (move='right' | move='left'))?
	(next?='next' toState=[State] | final?='terminate')? ';'
;

ReadWriteSymbol: symbol=[Symbol | SYMBOL_ID] | empty?='~';

terminal SYMBOL_ID: 
	'a'..'z' | 'A'..'Z' | '0'..'9' |
	'-' | '+' | '*' | '/' | '^' | '.' | '|' | '&' | '=' |
	'<' | '>' | '(' | ')' | '[' | ']'
;